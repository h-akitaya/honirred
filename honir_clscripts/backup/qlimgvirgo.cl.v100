#
#  qlimgvirgo.cl
#  HONIR Virgo image quick look
#
#           Ver 1.00 2013/01/26 H.Akitaya
#
#  usage: qlimgvirgo
#

procedure qlimgvirgo( _in_image )

string _in_image { prompt = "Image Name"}
string fn_dark = "_clscript$darktemplate201301.fits"
string fn_flat = "_clscript$flattemplate_H_201301.fits"
string file_out = "test_ql.fits"
bool flat = no
bool ds9 = no

begin

  string file_in, mainext, subext_in, subext_out, img_root, fn_out_trim, img
  string basedir, tmp_file, dscr

  mainext=".fits"
  subext_in=""
  subext_out="_btx"

  img = _in_image

  if( access( file_out ) ){
    print( "# Deleting old file..." )
    imdelete( file_out, ver- )
  }
  if( ! access( img ) ){

    error(1, "# File "//img//" not found !" )
  }
  extsrm( img, mainext, subext_in ) | scanf( "%s", img_root )
  fn_out_trim = img_root//subext_out//mainext

  if( access( fn_out_trim ) ){
    print( "# Deleting old file..." )
    imdelete( fn_out_trim, ver- )
  }

  hntrimvirgo( img, subext_in="", subext_out="_btx", >& "dev$null" )

  
  if (flat == yes ){
     tmp_file = mktemp("tmp$_qlimgvirgo_")
     imarith( fn_out_trim, "-", fn_dark, tmp_file )    
     imarith( tmp_file, "/", fn_flat, file_out )
     imdelete( tmp_file, ver- )
     dscr="dark subtracted, flattened"
  } else {
     imarith( fn_out_trim, "-", fn_dark, file_out )
     dscr="dark subtracted"
  }

  # clean up
  imdelete( fn_out_trim, ver-, >& "dev$null" )

  print("# Quick look image "//file_out//" ("//dscr//") created.")
  if (ds9 == yes ){
	xpaset( "-p ds9 frame 5" )
	xpaset( "-p ds9 file", file_out )  
  }
  print("# Done." )


end

# end of the script
